options {
    LOOKAHEAD = 1;
    STATIC = false;
    DEBUG_PARSER = false;
    DEBUG_LOOKAHEAD = false;
    DEBUG_TOKEN_MANAGER = false;
    KEEP_LINE_COLUMN = false;
    UNICODE_INPUT = true;
    SUPPORT_CLASS_VISIBILITY_PUBLIC = false;
}

PARSER_BEGIN(Parser)

package com.github.braisdom.jds;

import java.util.*;
import com.github.braisdom.jds.ast.*;

public final class Parser {

    public static String unquote(String value, String quote) {
        if (value.startsWith(quote) && (value.endsWith(quote))) {
            return value.substring(1, value.length() - 1);
        }
        return value;
    }
}

PARSER_END(Parser)

SKIP :
{
    " "
|   "\t"
|   "\n"
|   "\r"
|   "\f"
| <"//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <"/*" (~["*"])* "*" (~["/"] (~["*"])* "*")* "/">
| < "--" (~["\n","\r"])* ("\n" | "\r" | "\r\n") >
}

TOKEN :
{
    < EQ: "==" >
|   < GT: ">" >
|   < LT: "<" >
|   < LE: "<=" >
|   < GE: ">=" >
|   < NE: "!=" >
}

TOKEN :
{
    < SC_OR: "||" >
|   < SC_AND: "&&" >
}

TOKEN:
{
    < IN: "&=" >
|   < BETWEEN: "~=" >
|   < LIKE: "~" >
}

TOKEN:
{
   < PLUS: "+" >
|   < MINUS: "-" >
|   < STAR: "*" >
|   < SLASH: "/" >
|   < REM: "%" >
}

TOKEN :
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
}

TOKEN :
{
  < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
| < COLON: ":" >
| < QUOTE: "'" >
| < DOUBLE_QUOTE: "\"" >
}

TOKEN : {
    < STRINGVAL: ( "'" ( ~["'"] | ["\'"] )* "'") > |
    < DBSTRINGVAL: ( "\"" ( ~["\""] )* "\"") > |
    < BACK_QUOTED_IDENTIFIER: "`" ( (~["`"]) )+ "`" >
}

TOKEN : {
    < IMPORT : "import" > |
    < TYPEDEF : "typedef" > |
    < DATASET : "dataset" > |
    < PROJECTION : "projection" > |
    < FROM : "from" > |
    < PREDICATE : "predicate" > |
    < AS : "as" > |
    < RETURN : "return" > |
    < NULL : "null" >
}

TOKEN : {
    < JSYMBOL : "JSymbol" > |
    < JPROJECTION : "JProjection" > |
    < JTYPE : "JType" > |
    < JPREDICATE : "JPredicate" > |
    < JQuery : "JQuery" >
}


TOKEN : {
    < TYPE_NAME: (["A"-"Z"]) (<IDENTIFIER>)* > |
    < IDENTIFIER: <LETTER> (<LETTER> | <DIGIT>)* > |
    < #LETTER: ["_", "a"-"z", "A"-"Z"] > |
    < #DIGIT: ["0"-"9"] > |
    < DECIMAL_LITERAL: (["-"])? ["1"-"9"] (["0"-"9"])* > |
    < ZERO: "0" > |
    < FLOATING_LITERAL: (["-"])? (["0"-"9"])+ "." (["0"-"9"])*  >|
    < BOOLEAN_LITERAL: ("true" | "false") >
}

SqlNode SqlNode():
{
    final SqlNode sqlNode = new SqlNode();
    ImportNode importNode;
    DatasetNode datasetNode;
}
{
    (
        importNode = ImportNode() { sqlNode.addImportNode(importNode); } |
        datasetNode = DatasetNode() { sqlNode.addDatasetNode(datasetNode); }
    )* { return sqlNode; }
}

ImportNode ImportNode():
{
    ImportNode importNode;
    String importDeclaration;
}
{
    ( <IMPORT> importDeclaration = Name() { importNode = new ImportNode(importDeclaration); } <SEMICOLON> )
    {
        return importNode;
    }
}

DatasetNode DatasetNode():
{
    final DatasetNode datasetNode = new DatasetNode();
    FormalParameterNode parameter;
    Projectional projectional;
    FromNode fromNode;
    LogicExpression logicExpression;
}
{
    (
        <TYPEDEF> <DATASET> <TYPE_NAME> { datasetNode.setName(getToken(0).image); }
        "("
            parameter = FormalParameterNode() { datasetNode.addFormalParameter(parameter); }
            ( "," parameter = FormalParameterNode() { datasetNode.addFormalParameter(parameter); } )*
        ")"
        "{"
            <PROJECTION> ("[" Projectional(datasetNode) ( "," Projectional(datasetNode))* "]") <COMMA>
            <FROM> "("
                        (
                            fromNode = FromNode() { datasetNode.addFromNode(fromNode); }
                            ( <COMMA> fromNode = FromNode() { datasetNode.addFromNode(fromNode); } )*
                        )
                    ")"
            [ <COMMA> <PREDICATE> "(" logicExpression = LogicExpression() ")" { datasetNode.setPredicate(logicExpression); } ]
        "}"
    )
    { return datasetNode; }
}

FromNode FromNode():
{
    FromNode fromNode;
}
{
    (
        LOOKAHEAD(SqlFunctionCallNode()) fromNode = SqlFunctionCallNode() [ <AS> <IDENTIFIER> { fromNode.setAlias(token.image); } ] |
        LOOKAHEAD(SymbolNode()) fromNode = SymbolNode() [ <AS> <IDENTIFIER> { fromNode.setAlias(token.image); } ]
    ) { return fromNode; }
}

FormalParameterNode FormalParameterNode():
{
    String type;
    String name;
}
{
    (<TYPE_NAME> { type = getToken(0).image; } <IDENTIFIER> { name = getToken(0).image; })
    { return new FormalParameterNode(type, name); }
}

void Projectional(DatasetNode datasetNode):
{
    Projectional projectional = null;
    Token token;
}
{
    (
        LOOKAHEAD(ExpressionNode() <AS> <IDENTIFIER>) (projectional = ExpressionNode() <AS> token = <IDENTIFIER> { projectional.setAlias(token.image); } ) |
        LOOKAHEAD(SymbolNode()) (projectional = SymbolNode() [ <AS> token = <IDENTIFIER> { projectional.setAlias(token.image); } ] ) |
        LOOKAHEAD(SqlLiteral()) (projectional = SqlLiteral() [ <AS> token = <IDENTIFIER> { projectional.setAlias(token.image); } ] ) |
        (projectional = SqlFunctionCallNode() <AS> token = <IDENTIFIER> { projectional.setAlias(token.image); } )
    ) {  datasetNode.addProjectional(projectional); }
}

SqlFunctionCallNode SqlFunctionCallNode():
{
    SqlFunctionCallNode sqlFunctionCallNode = new SqlFunctionCallNode();
    SqlFunctionOperand operand;
}
{
    (
        [ LOOKAHEAD(2) ( <TYPE_NAME> { sqlFunctionCallNode.setType(token.image); } "." )]
        (<IDENTIFIER> | <TYPE_NAME> ) { sqlFunctionCallNode.setName(getToken(0).image); }
        "("
            [
                operand = SqlFunctionOperand() { sqlFunctionCallNode.addOperands(operand); }
                ( "," operand = SqlFunctionOperand() { sqlFunctionCallNode.addOperands(operand); } )*
            ]
        ")"
    ) { return sqlFunctionCallNode; }
}

SqlFunctionOperand SqlFunctionOperand():
{
    SqlFunctionOperand operand;
}
{
    (
        operand = SqlLiteral() { return operand; } |
        operand = SymbolNode() { return operand; } |
        operand = SqlFunctionCallNode() { return operand; }
    )
}

ExpressionNode ExpressionNode():
{
    ExpressionNode expressionNode = new ExpressionNode();
    ExpressionOperand left;
    String operator;
    ExpressionOperand right;
}
{
    (
        (
            left = ExpressionOperand() { expressionNode.setLeft(left); }
            operator = ExpressionOperator() { expressionNode.setOperator(operator); }
            right = ExpressionOperand() { expressionNode.setRight(right); }
        )*
    ) { return expressionNode; }
}

ExpressionOperand ExpressionOperand():
{
    ExpressionOperand operand = null;
    ExpressionNode expressionNode;
}
{
    (
        operand = SqlLiteral() { return operand; } |
        operand = SymbolNode() { return operand; } |
        "(" expressionNode = ExpressionNode() ")"
        {
            expressionNode.setEnclosed(true);
            return expressionNode;
        }
    )
}

LogicExpression LogicExpression():
{
    LogicExpression logicExpression = new LogicExpression();
    LogicExpressionOperand left;
    String operator;
    LogicExpressionOperand right;
}
{
    (
        (
            left = LogicExpressionOperand() { logicExpression.setLeft(left); }
            operator = LogicOperator() { logicExpression.setOperator(operator); }
            right = LogicExpressionOperand() { logicExpression.setRight(right); }
        )*
    ) { return logicExpression; }
}

LogicExpressionOperand LogicExpressionOperand():
{
    ExpressionOperand operand = null;
    LogicExpression logicExpression;
}
{
    (
        LOOKAHEAD(ExpressionOperand()) operand = ExpressionOperand() { return operand; } |
        "(" logicExpression = LogicExpression() ")"
        {
            logicExpression.setEnclosed(true);
            return logicExpression;
        }
    )
}

String ExpressionOperator():
{
}
{
    (
        <PLUS> { return token.image; } |
        <MINUS> { return token.image; } |
        <STAR> { return token.image; } |
        <SLASH> { return token.image; } |
        <REM> { return token.image; } |
        <SC_OR> { return token.image; } |
        <SC_AND> { return token.image; } |
        <LIKE> { return token.image; } |
        CompareOperator() { return token.image; }
    )
}

String LogicOperator() :
{

}
{
    <SC_AND> { return token.image; } |
    <SC_OR> { return token.image; } |
    CompareOperator() { return token.image; }
}

String CompareOperator() :
{

}
{
    <EQ> { return token.image; } |
    <GT> { return token.image; } |
    <GE> { return token.image; } |
    <NE> { return token.image; } |
    <LT> { return token.image; } |
    <LE> { return token.image; }
}

SqlLiteral SqlLiteral():
{
    Object value = null;
    Token token;
}
{
    ( token = <STRINGVAL> { value = token.image; } |
      token = <DECIMAL_LITERAL> { value = Integer.parseInt(token.image); } |
      token = <FLOATING_LITERAL> { value = Float.parseFloat(token.image); } |
      token = <ZERO> { value = Integer.parseInt(token.image); } |
      token = <NULL> { value = null; }
    ) { return new SqlLiteral(value); }
}

SymbolNode SymbolNode():
{
    SymbolNode symbolNode = new SymbolNode();
}
{
    (
        ( ":" <IDENTIFIER> ) { symbolNode.setSymbolName(token.image); } |
        ( "#" <IDENTIFIER> { symbolNode.setDatasetName(token.image); } [ "." <IDENTIFIER> { symbolNode.setSymbolName(token.image); } ] )
    )
    { return symbolNode; }
}

String Name():
{
    List<String> names = new ArrayList();
}
{
  ( <IDENTIFIER> { names.add(getToken(0).image); } ( <DOT> <IDENTIFIER> { names.add(getToken(0).image); })* )
  { return String.join(".", names.toArray(new String[0])); }
}
